plugins {
	id 'java'
	id 'idea'
	id "com.google.protobuf" version "0.8.8"
	id 'com.devsoap.vaadin-flow' version '1.1.1'
	id 'org.gretty' version '2.3.1'
}

group 'net.azry.p2p'
version '1.0-SNAPSHOT'

def grpcVersion = '1.20.0'
def protobufVersion = '3.7.1'
def protocVersion = protobufVersion
vaadin.version = '13.0.4'

vaadin.submitStatistics = false

sourceCompatibility = 1.8

vaadin.autoconfigure()

sourceSets {
	main {
		java {
			srcDirs 'build/generated/source/proto/main/java'
			srcDirs 'build/generated/source/proto/main/grpc'
		}
	}
}

repositories {
	mavenCentral()
	maven {
		url "http://maven.vaadin.com/vaadin-addons"
	}
}

dependencies {
	implementation "io.grpc:grpc-protobuf:${grpcVersion}"
	implementation "io.grpc:grpc-stub:${grpcVersion}"
	implementation "com.vaadin:vaadin-login-flow:1.0.0"
	implementation "org.apache.shiro:shiro-core:1.2.4"
	implementation "org.apache.shiro:shiro-web:1.2.4"
	implementation "ch.qos.logback:logback-classic:1.1.3"
	implementation "mysql:mysql-connector-java:8.0.15"

	implementation "org.vaadin.shiro:shiro-vaadin-integration:1.0.2"
	implementation "com.github.appreciated:app-layout-addon:3.0.0.beta4"

	compileOnly "javax.annotation:javax.annotation-api:1.2"

	// examples/advanced need this for JsonFormat
	implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"

	runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"

	testImplementation "io.grpc:grpc-testing:${grpcVersion}"
	testImplementation "junit:junit:4.12"
	testImplementation "org.mockito:mockito-core:2.25.1"
}

protobuf {
	protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
	plugins {
		grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
	}
	generateProtoTasks {
		all()*.plugins { grpc {} }
	}
}


clean {
	delete protobuf.generatedFilesBaseDir
}
