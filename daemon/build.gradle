plugins {
    id "com.google.protobuf" version "0.8.9"
    id "java"
    id 'application'
    id 'idea'
}

application {
    mainClassName = 'net.azry.p2pd.daemon.Main'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "io.grpc:grpc-netty-shaded:$grpcVersion"
    implementation "io.grpc:grpc-protobuf:$grpcVersion"
    implementation "io.grpc:grpc-stub:$grpcVersion"

    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    implementation project(':core')
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.google.inject:guice:4.2.2'
    implementation "com.github.atomashpolskiy:bt-core:$btVersion"
    implementation "com.github.atomashpolskiy:bt-http-tracker-client:$btVersion"
    implementation "com.github.atomashpolskiy:bt-dht:$btVersion"
//    implementation "com.github.atomashpolskiy:bt-upnp:$btVersion"

    testImplementation 'org.junit.jupiter:junit-jupiter:5.4.2'
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/java'
            srcDirs 'build/generated/source/proto/main/grpc'
        }
        proto {
            srcDirs '../protos'
        }
    }
}
protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

clean {
    delete protobuf.generatedFilesBaseDir
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}